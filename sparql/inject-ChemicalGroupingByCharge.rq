#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chemrof: <https://w3id.org/chemrof/>
PREFIX type: <http://www.wikidata.org/prop/direct/P31>
PREFIX subClassOf: <http://www.wikidata.org/prop/direct/P279>
PREFIX Isotope: <http://www.wikidata.org/entity/Q25276>
PREFIX Element: <http://www.wikidata.org/entity/Q11344>
PREFIX symbol: <http://www.wikidata.org/prop/direct/P246>
PREFIX atomic_number: <http://www.wikidata.org/prop/direct/P1086>
PREFIX neutron_number: <http://www.wikidata.org/prop/direct/P1148>

## TODO: generalize beyond elements

CONSTRUCT {
    ?iri a chemrof:ChemicalGroupingByCharge ;
      rdfs:label ?label ;
      chemrof:symbol ?symbol ;
      chemrof:has_charge_state ?charge_state ;
      chemrof:charge_agnostic_entity ?element
}
WHERE {
  ?element a chemrof:ChemicalElement ;
       chemrof:symbol ?element_symbol ;
       rdfs:label ?element_label .

  VALUES (?charge_state ?charge_label ?charge_symbol) {
     ( chemrof:Charged  "charged" "Ch")
     ( chemrof:Uncharged  "uncharged" "0")
     ( chemrof:AnionState  "anion" "-")
     ( chemrof:CationState  "cation" "+")
  }

  BIND(CONCAT(?element_label, " ", ?charge_label) AS ?label)
  BIND(CONCAT(?element_symbol, "(", ?charge_symbol, ")") AS ?symbol)
  BIND(IRI(CONCAT("https://w3id.org/chemrof/data/ChemicalGroupingByCharge/", STR(?element_symbol), "/", ?charge_symbol)) AS ?iri)

}

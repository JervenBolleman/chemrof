PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chem: <https://w3id.org/chemschema/>
PREFIX type: <http://www.wikidata.org/prop/direct/P31>
PREFIX subClassOf: <http://www.wikidata.org/prop/direct/P279>
PREFIX Isotope: <http://www.wikidata.org/entity/Q25276>
PREFIX Element: <http://www.wikidata.org/entity/Q11344>
PREFIX symbol: <http://www.wikidata.org/prop/direct/P246>
PREFIX atomic_number: <http://www.wikidata.org/prop/direct/P1086>
PREFIX neutron_number: <http://www.wikidata.org/prop/direct/P1148>

CONSTRUCT {
    chem:neutron_number a owl:DatatypeProperty .
    ?element a owl:Class .
    ?cls a owl:Class ;
         rdfs:label ?label ;
         owl:equivalentClass [
            owl:intersectionOf (
              ?element [
                 a owl:Restriction ;
                 owl:onProperty chem:neutron_number ;
                 owl:hasValue ?nn_int
                 ])
         ]
}
WHERE {
    ?cls a chem:Isotope ;
         rdfs:label ?label ;
         chem:neutron_number ?nn ;
         chem:has_element ?element .

    BIND(xsd:integer(?nn) AS ?nn_int)

}

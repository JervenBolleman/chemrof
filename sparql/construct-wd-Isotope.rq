# Isotopes from Wikidata
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chemrof: <https://w3id.org/chemrof/>
PREFIX type: <http://www.wikidata.org/prop/direct/P31>
PREFIX subClassOf: <http://www.wikidata.org/prop/direct/P279>
PREFIX Isotope: <http://www.wikidata.org/entity/Q25276>
PREFIX Element: <http://www.wikidata.org/entity/Q11344>
PREFIX symbol: <http://www.wikidata.org/prop/direct/P246>
PREFIX atomic_number: <http://www.wikidata.org/prop/direct/P1086>
PREFIX neutron_number: <http://www.wikidata.org/prop/direct/P1148>

CONSTRUCT {
    ?iri a chemrof:Isotope ;
      rdfs:label ?label ;
      chemrof:neutron_number ?nn ;
      chemrof:has_element ?element_iri
}
WHERE {
    ?i (type:/subClassOf:*) Isotope: ;
       rdfs:label ?label ;
       atomic_number: ?an ;
       neutron_number: ?nn .
    ?e type: Element: ;
       atomic_number: ?an ;
       symbol: ?element_symbol .

    FILTER (LANG(?label) = "en")

   BIND(IRI(CONCAT("https://w3id.org/chemrof/data/ChemicalElement/", STR(?element_symbol))) AS ?element_iri)
   BIND(IRI(CONCAT("https://w3id.org/chemrof/data/Isotope/", STR(?element_symbol), "/", STR(?nn))) AS ?iri)

}
